/-
Copyright (c) 2025 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import PhysLean.Relativity.PauliMatrices.Basic
import Mathlib.LinearAlgebra.CliffordAlgebra.Basic

/-!
# The Pauli matrices, interpreted as a Clifford algebra
-/
noncomputable section

namespace PauliMatrix

/-- The euclidean norm as a quadratic form. -/
@[simps!]
protected noncomputable def form : QuadraticForm ℝ (Fin 3 → ℝ) :=
  ∑ i, QuadraticMap.sq.comp (LinearMap.proj i)

/-- The injection from the Clifford algebra over `PauliMatrix.form` into the algebra generated by
the `σ` matrices. -/
def ofCliffordAlgebra :
    CliffordAlgebra PauliMatrix.form →ₐ[ℝ] Algebra.adjoin ℝ {σ1, σ2, σ3} :=
  CliffordAlgebra.lift _
    ⟨∑ i, (LinearMap.proj i).smulRight
      ⟨![σ1, σ2, σ3] i, Algebra.subset_adjoin <| by fin_cases i <;> simp⟩, fun v => by
      ext : 1
      simp only [Fin.isValue, Nat.succ_eq_add_one, Nat.reduceAdd, Fin.sum_univ_three,
        Matrix.cons_val_zero, Matrix.cons_val_one, Matrix.cons_val, LinearMap.add_apply,
        LinearMap.coe_smulRight, LinearMap.coe_proj, Function.eval, SetLike.mk_smul_mk,
        AddMemClass.mk_add_mk, MulMemClass.mk_mul_mk, mul_add, Algebra.mul_smul_comm, add_mul,
        Algebra.smul_mul_assoc, pauliMatrix_mul_self, σ2_mul_σ1, smul_neg, σ3_mul_σ1, smul_add,
        σ3_mul_σ2, form_apply, Fin.reduceFinMk, Algebra.algebraMap_eq_smul_one, SetLike.val_smul,
        OneMemClass.coe_one]
      module⟩

/-- The generators of the Clifford algebra correspond to the elements `σ`. -/
@[simp]
lemma ofCliffordAlgebra_ι_single (i : Fin 3) (r : ℝ) :
    ofCliffordAlgebra (CliffordAlgebra.ι _ (Pi.single i r)) =
        r • ⟨![σ1, σ2, σ3] i, Algebra.subset_adjoin <| by fin_cases i <;> simp⟩ :=
  CliffordAlgebra.lift_ι_apply _ _ _ |>.trans <| Subtype.ext <| by
    simp +contextual [Fintype.sum_eq_single i]

end PauliMatrix
