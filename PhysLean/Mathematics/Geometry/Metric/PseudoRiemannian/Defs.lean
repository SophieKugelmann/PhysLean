/-
Copyright (c) 2025 Matteo Cipollina. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Matteo Cipollina
-/

import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Geometry.Manifold.VectorBundle.SmoothSection
import Mathlib.LinearAlgebra.BilinearForm.Properties
import Mathlib.LinearAlgebra.QuadraticForm.Basic

/-!
# Pseudo-Riemannian Metrics on Smooth Manifolds

This file formalizes the fundamental notion of pseudo-Riemannian metrics on smooth manifolds
and establishes their basic properties. A pseudo-Riemannian metric equips a manifold with a
smoothly varying, non-degenerate, symmetric bilinear form on each tangent space, generalizing
the concept of an inner product space to curved spaces.

## Main Definitions

* `PseudoRiemannianMetric I n M`: A structure representing a `C^n` pseudo-Riemannian metric
  on a manifold `M` modelled on `(E, H)` with model with corners `I`. It consists of a family
  of non-degenerate, symmetric, continuous bilinear forms `gâ‚“` on each tangent space `Tâ‚“M`,
  varying `C^n`-smoothly with `x`. The base field `ğ•œ` is required to be `RCLike`, the model
  space `E` must be finite-dimensional, and the manifold `M` must be `C^{n+1}` smooth.

* `PseudoRiemannianMetric.flatEquiv g x`: The "musical isomorphism" from the tangent space at `x`
  to its dual space, representing the canonical isomorphism induced by the metric.

* `PseudoRiemannianMetric.sharpEquiv g x`: The inverse of the flat isomorphism, mapping from
  the dual space back to the tangent space.

## Implementation Notes

* The bilinear form `gâ‚“` at a point `x` is represented as a `ContinuousLinearMap`
  `val x : Tâ‚“M â†’L[ğ•œ] (Tâ‚“M â†’L[ğ•œ] ğ•œ)`. This curried form `v â†¦ (w â†¦ gâ‚“(v, w))` encodes both
  the bilinear structure and its continuity.

* Smoothness is defined by requiring that for any two `C^n` vector fields `X, Y`, the scalar
  function `x â†¦ gâ‚“(X x, Y x)` is `C^n` smooth (`ContMDiff n`).

* The manifold `M` must be `C^{n+1}` smooth (`IsManifold I (n + 1) M`) to ensure the tangent
  bundle is a `C^n` vector bundle, making the notion of `C^n` vector fields well-defined.

* Finite-dimensionality of the model space (`FiniteDimensional ğ•œ E`) guarantees that tangent
  spaces are finite-dimensional, which is necessary for establishing that non-degeneracy implies
  the existence of an isomorphism between the tangent space and its dual.
-/

section PseudoRiemannianMetric

noncomputable section

universe u v w

open Bundle Set Function Filter Module Topology ContinuousLinearMap
open scoped Manifold Bundle LinearMap Dual

/-- Convert a continuous linear map representing a bilinear form to a `LinearMap.BilinForm`. -/
def ContinuousLinearMap.toBilinForm
    {ğ•œ E : Type*} [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
    (f : E â†’L[ğ•œ] (E â†’L[ğ•œ] ğ•œ)) : LinearMap.BilinForm ğ•œ E :=
  LinearMap.mkâ‚‚ ğ•œ (fun v w => (f v) w)
    (fun vâ‚ vâ‚‚ w => by dsimp; rw [f.map_add]; rfl)
    (fun a v w => by dsimp; rw [f.map_smul]; rfl)
    (fun v wâ‚ wâ‚‚ => by dsimp; rw [(f v).map_add];)
    (fun a v w => (f v).map_smul a w)

-- We use Real-like fields for InnerProductSpace in Riemannian Metric
variable {ğ•œ : Type u} [RCLike ğ•œ] -- Stronger assumption, implies NontriviallyNormedField
variable {E : Type v} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [FiniteDimensional ğ•œ E]
variable {H : Type w} [TopologicalSpace H] -- Chart space
-- Manifold M and ChartedSpace for E
variable {M : Type w} [TopologicalSpace M] [ChartedSpace H M] [ChartedSpace H E]
variable {I : ModelWithCorners ğ•œ E H} {n : WithTop â„•âˆ} -- Model with corners and smoothness level

/-- A pseudo-Riemannian metric of smoothness class `C^n` on a manifold `M` modelled on `(E, H)`
    with model `I`. This structure defines a smoothly varying, non-degenerate, symmetric,
    continuous bilinear form `gâ‚“` on the tangent space `Tâ‚“M` at each point `x`.
    Requires `M` to be `C^{n+1}` smooth.
-/
@[ext]
structure PseudoRiemannianMetric
    (I : ModelWithCorners ğ•œ E H) (n : WithTop â„•âˆ)
    -- Instances ensuring the tangent bundle is a well-defined smooth vector bundle
    [TopologicalSpace (TangentBundle I M)]
    [FiberBundle E (TangentSpace I : M â†’ Type _)]
    [VectorBundle ğ•œ E (TangentSpace I : M â†’ Type _)]
    [IsManifold I (n + 1) M] -- Manifold is C^{n+1}
    -- Tangent bundle is C^n
    [ContMDiffVectorBundle n E (TangentSpace I : M â†’ Type _) I] : Type (max u v w) where
  /-- The metric tensor at each point `x : M`, represented as a continuous linear map
      `Tâ‚“M â†’L[ğ•œ] (Tâ‚“M â†’L[ğ•œ] ğ•œ)`. Applying it twice, `(val x v) w`, yields `gâ‚“(v, w)`. -/
  protected val : âˆ€ (x : M), TangentSpace I x â†’L[ğ•œ] (TangentSpace I x â†’L[ğ•œ] ğ•œ)
  /-- The metric is symmetric: `gâ‚“(v, w) = gâ‚“(w, v)`. -/
  protected symm : âˆ€ (x : M) (v w : TangentSpace I x), (val x v) w = (val x w) v
  /-- The metric is non-degenerate: if `gâ‚“(v, w) = 0` for all `w`, then `v = 0`. -/
  protected nondegenerate : âˆ€ (x : M) (v : TangentSpace I x), (âˆ€ w : TangentSpace I x,
    (val x v) w = 0) â†’ v = 0
  /-- The metric varies smoothly: `x â†¦ gâ‚“(Xâ‚“, Yâ‚“)` is a `C^n` function for `C^n`
      vector fields `X, Y`. -/
  protected smooth : âˆ€ (X Y : ContMDiffSection I E n (TangentSpace I)),
    ContMDiff I (modelWithCornersSelf ğ•œ ğ•œ) n (fun x => val x (X x) (Y x))

namespace PseudoRiemannianMetric

instance TangentSpace.addCommGroup (x : M) : AddCommGroup (TangentSpace I x) := by infer_instance
instance TangentSpace.module (x : M) : Module ğ•œ (TangentSpace I x) := by infer_instance

/-- If two vector spaces are linearly equivalent, and one is finite-dimensional,
    then so is the other. This version transfers the finite-dimensional property
    via a continuous linear equivalence. -/
lemma FiniteDimensional.of_equiv
    {ğ•œ : Type u} [RCLike ğ•œ]
    {E : Type v} [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
    {F : Type w} [AddCommGroup F] [Module ğ•œ F] [TopologicalSpace F]
    [FiniteDimensional ğ•œ E]
    (e : E â‰ƒL[ğ•œ] F) : FiniteDimensional ğ•œ F :=
  LinearEquiv.finiteDimensional e.toLinearEquiv

/-- If two vector spaces are linearly equivalent, and one is finite-dimensional,
    then so is the other. This version works with a linear equivalence. -/
lemma FiniteDimensional.of_linearEquiv
    {ğ•œ : Type u} [RCLike ğ•œ]
    {E : Type v} [AddCommGroup E] [Module ğ•œ E]
    {F : Type w} [AddCommGroup F] [Module ğ•œ F]
    [FiniteDimensional ğ•œ E]
    (e : E â‰ƒâ‚—[ğ•œ] F) : FiniteDimensional ğ•œ F := by
  exact Finite.equiv e

lemma VectorBundle.finiteDimensional_fiber
    (ğ•œ : Type u) [RCLike ğ•œ]
    {F : Type v} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [FiniteDimensional ğ•œ F]
    {B : Type w} [TopologicalSpace B]
    {E_bundle : B â†’ Type*} [âˆ€ x, AddCommGroup (E_bundle x)] [âˆ€ x, Module ğ•œ (E_bundle x)]
    [TopologicalSpace (Bundle.TotalSpace F E_bundle)]
    [âˆ€ x, TopologicalSpace (E_bundle x)]
    [FiberBundle F E_bundle] [VectorBundle ğ•œ F E_bundle]
    (x : B) : FiniteDimensional ğ•œ (E_bundle x) :=
  let triv := trivializationAt F E_bundle x
  let hx := mem_baseSet_trivializationAt F E_bundle x
  have h_linear : triv.IsLinear ğ•œ := VectorBundle.trivialization_linear' triv
  haveI : triv.IsLinear ğ•œ := h_linear
  let linear_equiv := triv.continuousLinearEquivAt ğ•œ x hx
  FiniteDimensional.of_equiv linear_equiv.symm

instance TangentSpace.finiteDimensional
  [FiniteDimensional ğ•œ E]
  [âˆ€ x : M, TopologicalSpace (TangentSpace I x)]
  [TopologicalSpace (Bundle.TotalSpace E (TangentSpace I : M â†’ Type _))]
  [FiberBundle E (TangentSpace I : M â†’ Type _)]
  [VectorBundle ğ•œ E (TangentSpace I : M â†’ Type _)]
  (x : M) :
    FiniteDimensional ğ•œ (TangentSpace I x) :=
  VectorBundle.finiteDimensional_fiber ğ•œ (F := E) (E_bundle := TangentSpace I) x

variable
    [inst_top : TopologicalSpace (TangentBundle I M)]
    [inst_fib : FiberBundle E (TangentSpace I : M â†’ Type _)]
    [inst_vec : VectorBundle ğ•œ E (TangentSpace I : M â†’ Type _)]
    [inst_mani : IsManifold I (n + 1) M]
    [inst_cmvb : ContMDiffVectorBundle n E (TangentSpace I : M â†’ Type _) I]

/-- Convert the continuous linear map representation `val x` to the algebraic `LinearMap.BilinForm`.
    This is useful for leveraging lemmas about `BilinForm`. -/
def toBilinForm
    (g : @PseudoRiemannianMetric ğ•œ _ E _ _ H _ M _ _ I n inst_top
         inst_fib inst_vec inst_mani inst_cmvb) (x : M) :
    LinearMap.BilinForm ğ•œ (TangentSpace I x) :=
  (g.val x).toBilinForm

/-- Coercion from PseudoRiemannianMetric to its function representation. -/
instance : CoeFun (@PseudoRiemannianMetric ğ•œ _ E _ _ H _ M _ _ I n inst_top
        inst_fib inst_vec inst_mani inst_cmvb)
        (fun _ => âˆ€ x : M, TangentSpace I x â†’L[ğ•œ] (TangentSpace I x â†’L[ğ•œ] ğ•œ)) where
           coe g := g.val

omit [FiniteDimensional ğ•œ E] [ChartedSpace H E] in
@[simp] lemma toBilinForm_apply
    (g : PseudoRiemannianMetric I n) (x : M) (v w : TangentSpace I x) :
  g.toBilinForm x v w = g.val x v w := rfl

omit [FiniteDimensional ğ•œ E] [ChartedSpace H E] in
@[simp] lemma toBilinForm_isSymm
    (g : PseudoRiemannianMetric I n) (x : M) : (g.toBilinForm x).IsSymm := by
  intro v w
  simp only [toBilinForm_apply]
  exact g.symm x v w

omit [FiniteDimensional ğ•œ E] [ChartedSpace H E] in
@[simp] lemma toBilinForm_nondegenerate
    (g : PseudoRiemannianMetric I n) (x : M) :
    (g.toBilinForm x).Nondegenerate := by
  intro v hv
  simp_rw [toBilinForm_apply] at hv
  exact g.nondegenerate x v hv

omit [FiniteDimensional ğ•œ E] [ChartedSpace H E] in
lemma symm'
    (g : PseudoRiemannianMetric I n) (x : M) (v w : TangentSpace I x) :
    (g.val x v) w = (g.val x w) v :=
  g.symm x v w

omit [FiniteDimensional ğ•œ E] [ChartedSpace H E] in
lemma nondegenerate'
    (g : PseudoRiemannianMetric I n) (x : M) (v : TangentSpace I x)
    (h : âˆ€ w : TangentSpace I x, (g.val x v) w = 0) : v = 0 :=
  g.nondegenerate x v h

omit [FiniteDimensional ğ•œ E] [ChartedSpace H E] in
lemma smooth'
    (g : @PseudoRiemannianMetric ğ•œ _ E _ _ H _ M _ _ I n inst_top
         inst_fib inst_vec inst_mani inst_cmvb) (X Y : ContMDiffSection I E n (TangentSpace I)) :
    ContMDiff I (modelWithCornersSelf ğ•œ ğ•œ) n (fun x => (g.val x (X x)) (Y x)) :=
  g.smooth X Y

/-- Convert a pseudo-Riemannian metric at a point to a quadratic form. -/
@[simp] def toQuadraticForm
    (g : @PseudoRiemannianMetric ğ•œ _ E _ _ H _ M _ _ I n inst_top
         inst_fib inst_vec inst_mani inst_cmvb) (x : M) :
    QuadraticForm ğ•œ (TangentSpace I x) where
  toFun v := g.val x v v
  toFun_smul a v := by
    simp only [ContinuousLinearMap.map_smul, ContinuousLinearMap.smul_apply, smul_smul, pow_two]
  exists_companion' :=
      âŸ¨ LinearMap.mkâ‚‚ ğ•œ (fun v y => g.val x v y + g.val x y v)
        (fun vâ‚ vâ‚‚ y => by -- Additivity in v
          simp only [ContinuousLinearMap.map_add, ContinuousLinearMap.add_apply]
          ring)
        (fun a v y => by -- Homogeneity in v
          simp only [ContinuousLinearMap.map_smul, ContinuousLinearMap.smul_apply]
          ring_nf
          exact Eq.symm (DistribSMul.smul_add a (((g.val x) v) y) (((g.val x) y) v)))
        (fun v yâ‚ yâ‚‚ => by -- Additivity in y
          simp only [ContinuousLinearMap.map_add, ContinuousLinearMap.add_apply]
          ring)
        (fun a v y => by -- Homogeneity in y
          simp only [ContinuousLinearMap.map_smul, ContinuousLinearMap.smul_apply]
          ring_nf
          exact Eq.symm (DistribSMul.smul_add a (((g.val x) v) y) (((g.val x) y) v))),
            by
              intro v y
              simp only [LinearMap.mkâ‚‚_apply, ContinuousLinearMap.map_add,
                ContinuousLinearMap.add_apply, g.symm x]
              ringâŸ©

omit [FiniteDimensional ğ•œ E] [ChartedSpace H E] in
lemma toQuadraticForm_apply
    (g : PseudoRiemannianMetric I n) (x : M) (v : TangentSpace I x) :
    g.toQuadraticForm x v = g.val x v v := rfl

/-- The "musical" isomorphism (index lowering) from the tangent space to its dual,
    induced by a pseudo-Riemannian metric. -/
def flat
    (g : @PseudoRiemannianMetric ğ•œ _ E _ _ H _ M _ _ I n inst_top
         inst_fib inst_vec inst_mani inst_cmvb) (x : M) :
    TangentSpace I x â†’â‚—[ğ•œ] (TangentSpace I x â†’L[ğ•œ] ğ•œ) :=
  { toFun := Î» v => g.val x v,
    map_add' := Î» v w => by simp only [ContinuousLinearMap.map_add],
    map_smul' := Î» a v => by simp only [ContinuousLinearMap.map_smul]; exact rfl }

omit [FiniteDimensional ğ•œ E] [ChartedSpace H E] in
@[simp] lemma flat_apply
    (g : PseudoRiemannianMetric I n) (x : M) (v w : TangentSpace I x) :
    (g.flat x v) w = g.val x v w := rfl

/-- The musical isomorphism as a continuous linear map. -/
def flatL
    (g : @PseudoRiemannianMetric ğ•œ _ E _ _ H _ M _ _ I n inst_top
         inst_fib inst_vec inst_mani inst_cmvb) (x : M) :
    TangentSpace I x â†’L[ğ•œ] (TangentSpace I x â†’L[ğ•œ] ğ•œ) :=
  { g.flat x with
    cont := ContinuousLinearMap.continuous (g.val x) }

omit [FiniteDimensional ğ•œ E] [ChartedSpace H E] in
@[simp] lemma flatL_apply
    (g : PseudoRiemannianMetric I n) (x : M) (v w : TangentSpace I x) :
    (g.flatL x v) w = g.val x v w := rfl

omit [FiniteDimensional ğ•œ E] [ChartedSpace H E] in
/-- The linear map `flat` is injective due to non-degeneracy. -/
@[simp] lemma flat_inj
    (g : PseudoRiemannianMetric I n) (x : M) :
    Function.Injective (g.flat x) := by
  rw [â† LinearMap.ker_eq_bot]
  apply LinearMap.ker_eq_bot'.mpr
  intro v hv
  apply g.nondegenerate' x v
  intro w
  exact DFunLike.congr_fun hv w

omit [FiniteDimensional ğ•œ E] [ChartedSpace H E] in
/-- The continuous linear map `flatL` is injective. -/
@[simp] lemma flatL_inj
    (g : PseudoRiemannianMetric I n) (x : M) :
    Function.Injective (g.flatL x) := by
  intro vâ‚ vâ‚‚ h
  apply flat_inj g x
  exact h

omit [ChartedSpace H E] in
/-- The continuous linear map `flatL` is surjective because the tangent space is
    finite dimensional. -/
@[simp] lemma flatL_surj
    (g : PseudoRiemannianMetric I n) (x : M) :
    Function.Surjective (g.flatL x) := by
  haveI : FiniteDimensional ğ•œ (TangentSpace I x) := TangentSpace.finiteDimensional x
  have h_finrank_eq : finrank ğ•œ (TangentSpace I x) = finrank ğ•œ (TangentSpace I x â†’L[ğ•œ] ğ•œ) := by
    have h_dual_eq : finrank ğ•œ (TangentSpace I x â†’L[ğ•œ] ğ•œ) = finrank ğ•œ (Module.Dual ğ•œ
    (TangentSpace I x)) := by
      let to_dual : (TangentSpace I x â†’L[ğ•œ] ğ•œ) â†’ Module.Dual ğ•œ (TangentSpace I x) :=
        fun f => f.toLinearMap
      let from_dual : Module.Dual ğ•œ (TangentSpace I x) â†’ (TangentSpace I x â†’L[ğ•œ] ğ•œ) := fun f =>
        ContinuousLinearMap.mk f (by
          apply LinearMap.continuous_of_finiteDimensional)
      let equiv : (TangentSpace I x â†’L[ğ•œ] ğ•œ) â‰ƒâ‚—[ğ•œ] Module.Dual ğ•œ (TangentSpace I x) :=
      { toFun := to_dual,
        invFun := from_dual,
        map_add' := fun f g => by
          ext v; unfold to_dual; simp only [LinearMap.add_apply]; rfl,
        map_smul' := fun c f => by
          ext v; unfold to_dual; simp only [LinearMap.smul_apply]; rfl,
        left_inv := fun f => by
          ext v; unfold to_dual from_dual; simp,
        right_inv := fun f => by
          ext v; unfold to_dual from_dual; simp }
      exact LinearEquiv.finrank_eq equiv
    rw [h_dual_eq, â† Subspace.dual_finrank_eq]
  exact (LinearMap.injective_iff_surjective_of_finrank_eq_finrank h_finrank_eq).mp (flatL_inj g x)

/-- The "musical" isomorphism (index lowering) from the tangent space to its dual,
    as a continuous linear equivalence. -/
@[simp] def flatEquiv
    (g : @PseudoRiemannianMetric ğ•œ _ E _ _ H _ M _ _ I n inst_top
         inst_fib inst_vec inst_mani inst_cmvb)
    (x : M) :
    TangentSpace I x â‰ƒL[ğ•œ] (TangentSpace I x â†’L[ğ•œ] ğ•œ) :=
  LinearEquiv.toContinuousLinearEquiv
    (LinearEquiv.ofBijective
      ((g.flatL x).toLinearMap)
      âŸ¨g.flatL_inj x, g.flatL_surj xâŸ©)

omit [ChartedSpace H E] in
lemma coe_flatEquiv
    (g : PseudoRiemannianMetric I n) (x : M) :
    (g.flatEquiv x : TangentSpace I x â†’â‚—[ğ•œ] (TangentSpace I x â†’L[ğ•œ] ğ•œ)) = g.flatL x := rfl

omit [ChartedSpace H E] in
@[simp] lemma flatEquiv_apply
    (g : PseudoRiemannianMetric I n) (x : M) (v w : TangentSpace I x) :
    (g.flatEquiv x v) w = g.val x v w := rfl

/-- The "musical" isomorphism (index raising) from the dual of the tangent space to the
    tangent space, induced by a pseudo-Riemannian metric. This is the inverse of `flatEquiv`. -/
def sharpEquiv
    (g : @PseudoRiemannianMetric ğ•œ _ E _ _ H _ M _ _ I n inst_top
         inst_fib inst_vec inst_mani inst_cmvb) (x : M) :
    (TangentSpace I x â†’L[ğ•œ] ğ•œ) â‰ƒL[ğ•œ] TangentSpace I x :=
  (g.flatEquiv x).symm
#lint
/-- The index raising map `sharp` as a continuous linear map. -/
def sharpL
    (g : @PseudoRiemannianMetric ğ•œ _ E _ _ H _ M _ _ I n inst_top
         inst_fib inst_vec inst_mani inst_cmvb) (x : M) :
    (TangentSpace I x â†’L[ğ•œ] ğ•œ) â†’L[ğ•œ] TangentSpace I x :=
  (g.sharpEquiv x).toContinuousLinearMap

omit [ChartedSpace H E] in
lemma sharpL_eq_toContinuousLinearMap
    (g : PseudoRiemannianMetric I n) (x : M) :
  g.sharpL x = (g.sharpEquiv x).toContinuousLinearMap := rfl

omit [ChartedSpace H E] in
lemma coe_sharpEquiv
    (g : PseudoRiemannianMetric I n) (x : M) :
    (g.sharpEquiv x : (TangentSpace I x â†’L[ğ•œ] ğ•œ) â†’L[ğ•œ] TangentSpace I x) = g.sharpL x := rfl

/-- The index raising map `sharp` as a linear map. -/
noncomputable def sharp
    (g : @PseudoRiemannianMetric ğ•œ _ E _ _ H _ M _ _ I n inst_top
    inst_fib inst_vec inst_mani inst_cmvb) (x : M) :
    (TangentSpace I x â†’L[ğ•œ] ğ•œ) â†’â‚—[ğ•œ] TangentSpace I x :=
  (g.sharpEquiv x).toLinearEquiv.toLinearMap

omit [ChartedSpace H E] in
@[simp] lemma sharpL_apply_flatL
    (g : PseudoRiemannianMetric I n) (x : M) (v : TangentSpace I x) :
    g.sharpL x (g.flatL x v) = v :=
  (g.flatEquiv x).left_inv v

omit [ChartedSpace H E] in
@[simp] lemma flatL_apply_sharpL
    (g : PseudoRiemannianMetric I n) (x : M) (Ï‰ : TangentSpace I x â†’L[ğ•œ] ğ•œ) :
    g.flatL x (g.sharpL x Ï‰) = Ï‰ :=
  (g.flatEquiv x).right_inv Ï‰

omit [ChartedSpace H E] in
/-- Applying `sharp` then `flat` recovers the original covector. -/
@[simp] lemma flat_sharp_apply
    (g : PseudoRiemannianMetric I n) (x : M) (Ï‰ : TangentSpace I x â†’L[ğ•œ] ğ•œ) :
    g.flat x (g.sharp x Ï‰) = Ï‰ := by
  -- Use the continuous versions and coerce
  have := flatL_apply_sharpL g x Ï‰
  -- Need to relate flat and flatL, sharp and sharpL
  simp only [sharp, sharpL, flat, flatL, coe_flatEquiv]; simp only [coe_sharpEquiv,
             ContinuousLinearEquiv.coe_coe, LinearEquiv.coe_coe] at this âŠ¢
  exact this

omit [ChartedSpace H E] in
@[simp] lemma sharp_flat_apply
    (g : PseudoRiemannianMetric I n) (x : M) (v : TangentSpace I x) :
    g.sharp x (g.flat x v) = v := by
  -- Use the continuous versions and coerce
  have := sharpL_apply_flatL g x v
  simp only [sharp, sharpL, flat, flatL]; simp only [coe_flatEquiv, coe_sharpEquiv,
             ContinuousLinearEquiv.coe_coe, LinearEquiv.coe_coe] at this âŠ¢
  exact this

omit [ChartedSpace H E] in
/-- The metric evaluated at `sharp Ï‰â‚` and `sharp Ï‰â‚‚`. -/
@[simp] lemma apply_sharp_sharp
    (g : PseudoRiemannianMetric I n) (x : M) (Ï‰â‚ Ï‰â‚‚ : TangentSpace I x â†’L[ğ•œ] ğ•œ) :
    g.val x (g.sharpL x Ï‰â‚) (g.sharpL x Ï‰â‚‚) = Ï‰â‚ (g.sharpL x Ï‰â‚‚) := by
  rw [â† flatL_apply g x (g.sharpL x Ï‰â‚)]
  rw [flatL_apply_sharpL g x Ï‰â‚]

omit [ChartedSpace H E] in
/-- The metric evaluated at `v` and `sharp Ï‰`. -/
lemma apply_vec_sharp
    (g : PseudoRiemannianMetric I n) (x : M) (v : TangentSpace I x) (Ï‰ : TangentSpace I x â†’L[ğ•œ] ğ•œ) :
    g.val x v (g.sharpL x Ï‰) = Ï‰ v := by
  rw [g.symm' x v (g.sharpL x Ï‰)]
  rw [â† flatL_apply g x (g.sharpL x Ï‰)]
  rw [flatL_apply_sharpL g x Ï‰]

end PseudoRiemannianMetric
